// SPDX-License-Identifier: MIT
// Copyright © 2022 André Santos

start: file

///////////////////////////////////////////////////////////////////////////////
// Source Files

file: _file_element*

_file_element: command_invocation
             | comment


///////////////////////////////////////////////////////////////////////////////
// Command Invocations

command_invocation: _identifier _parenthesized_arguments

_identifier: CNAME

_parenthesized_arguments: "(" _arguments ")"

_arguments: _commented_arguments*

_commented_arguments: comment
                    | argument
                    | _parenthesized_arguments


///////////////////////////////////////////////////////////////////////////////
// Command Arguments

?argument: bracket_argument | quoted_argument | unquoted_argument

//// Bracket Argument ////

bracket_argument: BRACKET_ARGUMENT

BRACKET_ARGUMENT: /\[(=*)\[(.*?)\]\1\]/s

//// Quoted Argument ////

quoted_argument: EMPTY_STRING | QUOTED_ARGUMENT

EMPTY_STRING: "\"\""

QUOTED_ARGUMENT: /"(.*?)(?=[^\\]")."/s

//// Unquoted Argument ////

unquoted_argument: UNQUOTED_ARGUMENT
//               | unquoted_legacy

UNQUOTED_ARGUMENT: UNQUOTED_ELEMENT+

UNQUOTED_ELEMENT: /[^\s#\\"()]/
                | ESCAPE_SEQUENCE

// unquoted_legacy: TODO


///////////////////////////////////////////////////////////////////////////////
// Escape Sequences

ESCAPE_SEQUENCE: ESCAPE_IDENTITY
               | ESCAPE_ENCODED
               | ESCAPE_SEMICOLON

ESCAPE_IDENTITY: /\\[^A-Za-z0-9;]/

ESCAPE_ENCODED: "\\t" | "\\r" | "\\n"

ESCAPE_SEMICOLON: "\\;"


///////////////////////////////////////////////////////////////////////////////
// Comments

?comment: bracket_comment
        | line_comment

bracket_comment: BRACKET_COMMENT

line_comment: LINE_COMMENT

BRACKET_COMMENT.2: /#\[(=*)\[(.*?)\]\1\]/s

LINE_COMMENT.2: /#(?!\[=*\[)[^\r\n]*/


///////////////////////////////////////////////////////////////////////////////
// Imports

%import common.WS
%import common.CNAME
%ignore WS
